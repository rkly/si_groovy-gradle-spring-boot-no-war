buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE')
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

plugins {
    id "net.saliman.cobertura" version "2.2.8"
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'cobertura'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

configurations { providedRuntime }

version = 1.0
description = 'Little Spring Boot project to play around.'

repositories { mavenCentral() }

processResources { expand(project.properties) }

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    compile("org.springframework.boot:spring-boot-starter-thymeleaf:1.4.2.RELEASE")

    compile('org.springframework.boot:spring-boot-starter-web:1.4.2.RELEASE') {
        exclude module: 'spring-boot-starter-tomcat' 
    }
    compile('org.springframework.boot:spring-boot-starter-jetty:1.4.2.RELEASE')
    compile('org.springframework.boot:spring-boot-starter-actuator:1.4.2.RELEASE')
    providedRuntime("org.springframework.boot:spring-boot-starter-jetty")

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4'
    testCompile 'org.springframework.boot:spring-boot-starter-test:1.4.2.RELEASE'
    testCompile 'junit:junit:4.12'
}

jar {
    baseName = this.name
    version =  this.version
}

test {
    include "**/*Test*"
    reports.html.enabled = false
    afterTest { desc, result ->
        println "Executing '${desc.name}' [${desc.className}] with result: ${result.resultType}"
    }
}

task componentTests(type: Test, dependsOn: test) {
    include "**/*CT*"
    reports.html.enabled = false
    afterTest { desc, result ->
        println "Executing '${desc.name}' [${desc.className}] with result: ${result.resultType}"
    }
}

task allTests(type: TestReport, dependsOn: ['test', 'componentTests']) {
    doFirst {
        gradle.startParameter.continueOnFailure = true
    }
    destinationDir = file("$buildDir/reports/allTests")
    tasks.withType(Test) { reportOn it }
}

check {
    dependsOn = [
        'test',
        'componentTests',
        'allTests'
    ]
}

cobertura {
    coverageSourceDirs = sourceSets.main.groovy.srcDirs
    coverageFormats = ['html', 'xml']
}
